import org.apache.tools.ant.filters.ReplaceTokens

apply plugin: 'java'
apply plugin: 'findbugs'

sourceCompatibility = '1.7'
targetCompatibility = '1.7'

archivesBaseName = 'theta-gui'
version = '0.1'

repositories {
    mavenCentral()
}

dependencies {
    compile project(':lib')
    compile group: 'org.slf4j', name: 'slf4j-api', version: '1.7.9'
    compile group: 'ch.qos.logback', name: 'logback-classic', version: '1.1.2'
    testCompile group: 'junit', name: 'junit', version: '4.11'
}

jar {
    manifest {
        attributes 'Implementation-Title': 'RICOH Theta GUI tool'
        attributes 'Implementation-Version': version
        attributes "Main-Class": "com.theta360.gui.ThetaGUI"
    }
    from configurations.compile.collect { it.isDirectory() ? it : zipTree(it) }
}

task scripts(type: Copy, dependsOn: jar) {
    from 'src/main/script'
    into 'build/bin'
    filter(ReplaceTokens, tokens: [ARCHIVE_BASE_NAME: archivesBaseName, VERSION: version])
}

task distribute(type: Copy, dependsOn: scripts) {
    from "build/libs"
    into "build/bin"
}
